import numpy as np
import os
from setuptools import Distribution, Extension

Import('env')

# make sure numpy headers are available
env.AppendUnique(CPPPATH=[np.get_include()])

# configure the boost_python library, which may have different extensions
if not (GetOption('clean') or env.GetOption('help')):
    SConscript('SConscript.configure')

# python extension module
module = env.SharedLibrary(
    '_pyobjcryst', 
    Glob('*.cpp'),
    SHLIBPREFIX='', 
    SHLIBSUFFIX='.so')

installed = env.Install(Dir('#/src/pyobjcryst'), module)

# run `scons develop` to install the extension in development mode
dev = Alias('dev', installed)
AlwaysBuild(dev)


# run `scons test` to run the tests
test = env.Alias(
    'test', 
    ['dev'],
    f'PYTHONPATH={Dir("#").abspath + "/src"} python -m pyobjcryst.tests.run')
AlwaysBuild(test)

# default targets:
Default(module)

# vim: ft=python
